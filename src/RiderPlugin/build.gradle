plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.2.50'
    id 'org.jetbrains.intellij' version '0.3.2'
}

version = version != 'unspecified' ? version : '0.0.0.1'

if (!project.hasProperty('configuration')) ext.configuration = 'Release'

intellij {
    type = 'RD'
    // https://www.jetbrains.com/intellij-repository/releases
    // version = '173-SNAPSHOT'
    version = "2018.3-SNAPSHOT"
    downloadSources = false
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

compileKotlin {
    kotlinOptions { jvmTarget = "1.8" }
}

prepareSandbox {
    from("$projectDir/../$gradle.resharperPluginProjectName/bin/RD/$configuration", {
        into "$intellij.pluginName/dotnet"
        include "*$gradle.resharperPluginProjectName*"
    })
}

patchPluginXml {
    def historyText = file("$projectDir/../../CHANGELOG.md").text
    def historyMatches = historyText =~ /(?s)###(.+?)###(.+?)(?=###|$)/

    changeNotes = historyMatches.collect {
        def versionTitle = it[1]
        def versionText = it[2].replaceAll(/(?s)\r?\n/, "<br />\n")
        "<b>$versionTitle</b>$versionText"
    }.take(10).join('')

    //sinceBuild = "1xx.*"
}

runIde {
    configDirectory = System.properties['user.home'] + '/.RiderPluginDev/config'
    systemDirectory = System.properties['user.home'] + '/.RiderPluginDev/system'
}
